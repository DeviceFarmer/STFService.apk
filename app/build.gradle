apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.9.3'
    }
}

android {
    compileSdkVersion 33

    signingConfigs {
        debug {
            storeFile file('keys/debug.keystore')
            storePassword 'insecure-store-password'
            keyAlias 'STFDebugKey'
            keyPassword 'insecure-key-password'
        }
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 11
        versionName "2.4.10"
        applicationId "jp.co.cyberagent.stf"
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.debug
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    lintOptions {
        informational 'ExpiredTargetSdkVersion'
    }
}

dependencies {
    //TODO consider migrating to androidx
    //noinspection GradleCompatible
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.google.protobuf:protobuf-lite:3.0.1'
    compileOnly fileTree(dir: '../shim/libs', include: ['*.jar'])
}

import org.apache.tools.ant.taskdefs.condition.Os
def archSuffix = Os.isFamily(Os.FAMILY_MAC) ? ':osx-x86_64' : ''
protobuf {
    protoc {
      artifact = "com.google.protobuf:protoc:3.21.12$archSuffix"
    }
    plugins {
      javalite {
        artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0$archSuffix"
      }
    }
    generateProtoTasks {
      all().each { task ->
        task.plugins {
          javalite {}
        }
      }
    }
}
